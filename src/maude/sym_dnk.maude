load dnk_vc.maude

fmod DNA_VC_MAP is
  protecting VECTOR_CLOCK_MAP .
  protecting DNA .

  sort DNA_VC_MAP .

  --- DNA term + map of vector clocks
  op dnaVCMap(_,_) : DNA VCMap -> DNA_VC_MAP .

  --- DNA term + key of corresponding vector clock in a map
  op vc : DNA Nat -> DNA_VC .
endfm

view DNA_VC_MAP from TRIV to DNA_VC_MAP is
  sort Elt to DNA_VC_MAP .
endv

fmod SYM_DNA is 
  protecting DNA_VC_MAP .
  protecting LIST {DNA_VC_MAP} * (sort List {DNA_VC_MAP} to DNAVCList) .

	op _||_ : DNA_VC DNA_VC -> DNA_VC [ctor comm metadata "parallel composition" prec 42 gather (E e)] .
	op _|_ : DNA_VC DNA_VC -> DNA_VC [ctor comm prec 42 gather (E e)] .
	op _||<_ : DNA_VC DNA_VC -> DNA_VC [ctor metadata "left merge" prec 42 gather (e E)] .

	op rd{_}(_,_,_) : Nat DNA_VC VCMap DNA_VC_MAP -> DNAVCList .

  var Trace : DNAVCList . 
  var VCs : VCMap .
  var X : Channel .
  vars P Q : DNA_VC .
  vars U V : DNA .
  var D I J : Nat .
  var N : String .

  eq rd{0}(P, VCs, Trace) = nil .
  eq rd{D}(vc(bot, I), VCs, Trace) = nil .
	---A5: added comm to _||_
	---A6: added assoc to _||_

	--- eq [A7] : (P || bot) = P .
  eq [A7] : rd{D}((P || vc(bot, I)), VCs, Trace) = rd{D}(P, VCs, Trace) .

	--- ceq [A8] : (P || Q) = (P ||< Q) o+ (Q ||< P) o+ (P | Q) if not (P :: Recursive) /\ not (P :: UnguardedTerm) /\ not (Q :: Recursive) /\ not (Q :: UnguardedTerm) .
  ceq [A8] : rd{D}((P || Q), VCs, Trace)
    = if rd{D}((P ||< Q), VCs, Trace) =/= nil 
    then rd{D}((P ||< Q), VCs, Trace)
    else if rd{D}((Q ||< P), VCs, Trace) =/= nil
      then rd{D}((Q ||< P), VCs, Trace)
      else rd{D}((P | Q), VCs, Trace)
      fi
    fi if not (P :: RecursiveVC) /\ not (P :: UnguardedTerm) /\ not (Q :: RecursiveVC) /\ not (Q :: UnguardedTerm) .

	--- eq [A9] : (bot ||< P) = bot .
  eq [A9] : rd{D}((vc(bot, I) || P), VCs, Trace) = rd{D}(vc(bot, I), VCs, Trace) .

	--- eq [A10-netkat] : (N ; P) ||< Q = N ; (P || Q) .
  eq [A10-netkat] : rd{s(D)}((vc(N ; U, I) ||< Q), VCs, Trace) = rd{D}((vc(U, I) || Q), inc(VCs, I), append(Trace, dnaVCMap(N ; bot, inc(VCs, I)))) .

	--- eq [A10-send] : ((X ! N) ; Q) ||< R = ((X ! N) ; (Q || R)) .
  eq [A10-send] : rd{s(D)}((vc((X ! N) ; U, I) ||< Q), VCs, Trace) = rd{D}((vc(U, I) || Q), VCs, Trace) . ---- Sending alone does not count in the trace

	--- eq [A10-receive] : ((X ? N) ; Q) ||< R = ((X ? N) ; (Q || R)) .
  eq [A10-receive] : rd{s(D)}((vc((X ? N) ; U, I) ||< Q), VCs, Trace) = rd{D}((vc(U, I) || Q), VCs, Trace) . ---- Receiving alone does not count in the trace

	--- eq [A10-rcfg] : ((rcfg(X, N)) ; Q) ||< R = ((rcfg(X, N)) ; (Q || R)) .
  --- A12 appends the rcfg directly to the trace, so A10-rcfg is not needed anymore

	--- eq [A11] : (P o+ Q) ||< R = (P ||< R) o+ (Q ||< R) .
  eq [A11] : rd{D}((vc(U o+ V, I) ||< Q), VCs, Trace)
    = if rd{D}((vc(U, I) ||< Q), VCs, Trace) =/= nil
    then rd{D}((vc(U, I) ||< Q), VCs, Trace)
    else rd{D}((vc(V, I) ||< Q), VCs, Trace)
    fi .

	--- eq [A12] : ((X ? N) ; Q) | ((X ! N) ; R) = (rcfg(X, N)) ; (Q || R) .
  eq [A12] : rd{s(D)}((vc((X ? N) ; U, I) | vc((X ! N) ; V, J)), VCs, Trace) = rd{D}((vc(U, I) || vc(V, J)),
                                                                                     transferVC(VCs, J, I),
                                                                                     append(Trace, dnaVCMap(rcfg(X, N) ; bot, transferVC(VCs, J, I)))
                                                                                    ) .

	--- eq [A13] : (P o+ Q) | R = (P | R) o+ (Q | R) .
  eq [A13] : rd{D}((vc(U o+ V, I) | Q), VCs, Trace)
    = if rd{D}((vc(U, I) | Q), VCs, Trace) =/= nil
    then rd{D}((vc(U, I) | Q), VCs, Trace)
    else rd{D}((vc(V, I) | Q), VCs, Trace)
    fi .

	---A14: added comm to _|_

	--- ceq [A15] : P | Q = bot if not (P :: Recursive) /\ not (P :: UnguardedTerm) /\ not (Q :: Recursive) /\ not (Q :: UnguardedTerm) [owise] .
	ceq [A15] : rd{D}((P | Q), VCs, Trace) = nil if not (P :: RecursiveVC) /\ not (P :: UnguardedTerm) /\ not (Q :: RecursiveVC) /\ not (Q :: UnguardedTerm) .

	--- eq [cr-1] : P ||< bot = P .
  eq [cr-1] : rd{D}((P ||< vc(bot, I)), VCs, Trace) = rd{D}(P, VCs, Trace) .
endfm

fmod RECURSIVE_SYM_DNA is
	protecting SYM_DNA .

  var Trace : DNAVCList . 
	var C : Comm .
	var N : String .
  var D I : Nat .
	vars P Q : DNA_VC .
  var U : DNA .
  var VCs : VCMap .

	op getRecPol(_) : Recursive -> DNA .
	op unfold(_) : DNA_VC -> DNA_VC .
	op notUnfolded(_) : DNA_VC -> Bool .


	---unfolding the recursive variables at once is very inefficient.
	---we perform the unfolding on the fly when it is necessary
	---i.e. when the index of rd operator is still a positive integer
	---and the execution cannot continue because the terms are not guarded.
	
	ceq unfold(P || Q) = (unfold(P) || Q) if (P :: UnguardedTerm) .
	ceq unfold(P ||< Q) = (unfold(P) ||< Q) if (P :: UnguardedTerm) .
	ceq unfold(P | Q) = (unfold(P) | Q) if (P :: UnguardedTerm) .
	ceq unfold(vc(U, I)) = vc(getRecPol(U), I) if (U :: Recursive) .

	eq notUnfolded(unfold(P)) = false .
	eq notUnfolded(P) = true [owise] .

	ceq [rec-1] : rd{s(D)}(P, VCs, Trace) = rd{s(D)}(unfold(P), VCs, Trace) if notUnfolded(P) [owise] .
	cmb [rec-2] : (P || Q) : UnguardedTerm if ((P :: RecursiveVC) or (P :: UnguardedTerm)) .
	cmb [rec-3] : (P ||< Q) : UnguardedTerm if ((P :: RecursiveVC) or (P :: UnguardedTerm)) .
  cmb [rec-4] : vc(U, I) : RecursiveVC if (U :: Recursive) .
endfm
