load vector_clock.maude

fmod TRACE_TREE is
  protecting VECTOR_CLOCK_MAP .

  sorts TraceNode TraceNodes TNType .

  op TLeaf : -> TraceNode .
  ---- A trace node contains a transition, a vector clock map,
  ---- and the next neighbors the node is connected to in the trace
  op TNode(_,_,_,_) : TNType String VCMap TraceNodes -> TraceNode .
  ---- empty list of nodes
  op TEmpty : -> TraceNodes .
  op TAppend(_,_) : TraceNode TraceNodes -> TraceNodes .
  op TConcat(_,_) : TraceNodes TraceNodes -> TraceNodes .

  op TDR : -> TNType . ---- data race
  op TSTART : -> TNType . ---- start node 
  op TNORMAL : -> TNType . ---- normal node 
  op TPRUNED : -> TNType . ---- normal node 

  var TN : TraceNode .
  var Nodes RemNodes : TraceNodes .

  eq TConcat(TEmpty, Nodes) = Nodes .
  eq TConcat(TAppend(TN, RemNodes), Nodes) = TConcat(RemNodes, TAppend(TN, Nodes)) .
endfm

fmod TRACE_TREE_TO_DOT is
  protecting TRACE_TREE .
  protecting CONVERSION .
  protecting EXT-BOOL .

  op TraceToDOT(_) : TraceNodes -> String .
  op TraceToDOTAux(_,_,_) : Nat String TraceNodes -> String .
  op declareNode(_,_,_,_) : TNType Nat String VCMap -> String .
  op declareEdge(_,_,_) : String String String -> String .
  op getNodeId(_,_) : Nat String -> String .
  op getNodeColor(_) : TNType -> String .

  var T : TNType .
  var N : Nat .
  var S PID From To : String .
  var VCs : VCMap .
  var Nodes RemNodes : TraceNodes .

  ---- purple #9068be 
  eq getNodeColor(TSTART) = "#6ed37e" .
  eq getNodeColor(TPRUNED) = "#9068be" .
  eq getNodeColor(TDR) = "#e62739" .
  eq getNodeColor(T) = "#e1e8f0" [owise] .

  eq getNodeId(N, PID) = if PID == ""
                         then "n" + string(N, 10)
                         else PID + string(N, 10)
                         fi .

  eq declareNode(T, N, PID, VCs) = getNodeId(N, PID) + " [label=\"" + VCMapToString(VCs) + 
                                   "\", shape=rectangle, style=filled, fillcolor=\"" +
                                   getNodeColor(T) + "\"];\n" .

  eq declareEdge(From, To, S) = if ((From =/= "") and-then (To =/= ""))
                                then From + " -> " + To + "[label=\"" + S + "\"];\n"
                                else ""
                                fi .

  eq TraceToDOT(Nodes) = "digraph G {\n" + TraceToDOTAux(0, "", Nodes) + "}" .
  
  eq TraceToDOTAux(N, PID, TEmpty) = "" .
  eq TraceToDOTAux(N, PID, TAppend(TLeaf, RemNodes)) = TraceToDOTAux(N, PID, RemNodes) .
  eq TraceToDOTAux(N, PID, TAppend(TNode(T, S, VCs, Nodes), RemNodes)) = declareNode(T, N, PID, VCs) +
                                                                          declareEdge(PID, getNodeId(N, PID), S) +
                                                                          TraceToDOTAux(N, getNodeId(N, PID), Nodes) +
                                                                          TraceToDOTAux(s(N), PID, RemNodes) .
endfm
